**************************************************************************
*   Method attributes.                                                   *
**************************************************************************
Instantiation: Public
**************************************************************************

  METHOD set_po_initiator_text.
    DATA: lt_subcontainer     TYPE STANDARD TABLE OF swr_cont,
          lt_simple_container TYPE STANDARD TABLE OF swr_cont,
          lv_sy_subrc         TYPE sy-subrc,
          lv_xml_container    TYPE xstring,
          lv_xml_schema       TYPE xstring.
    DATA: lv_total_lineitem    TYPE string,
          lv_po_date           TYPE string,
          lv_approval_comments TYPE string,
          lv_level             TYPE i,
          lv_level_s           TYPE string.
    DATA: lt_callstack TYPE  abap_callstack,
          et_callstack TYPE  sys_callst.
*--------------------------------------------------------------------*

    CALL FUNCTION 'SAP_WAPI_READ_CONTAINER'
      EXPORTING
        workitem_id              = workitem_id "l_wi_header-wi_chckwi
      IMPORTING
        return_code              = lv_sy_subrc
        ifs_xml_container        = lv_xml_container
        ifs_xml_container_schema = lv_xml_schema
      TABLES
        simple_container         = lt_simple_container[]
        subcontainer_all_objects = lt_subcontainer[].

    IF lt_subcontainer[] IS NOT INITIAL.
      IF line_exists( lt_subcontainer[ element = '_WI_OBJECT_ID' ] ).
        DATA(purchase_order) = CONV ebeln( lt_subcontainer[ element = '_WI_OBJECT_ID' ]-value+0(10) ).
      ELSEIF line_exists( lt_subcontainer[ element = 'PURCHASEORDER' ] ).
        purchase_order = CONV ebeln( lt_subcontainer[ element = 'PURCHASEORDER' ]-value+0(10) ).
      ENDIF.
      IF purchase_order IS NOT INITIAL.
*-- Calculate total amount in PR
        SELECT FROM ekpo AS a INNER JOIN ekko AS b ON a~ebeln = b~ebeln
                              INNER JOIN lfa1 AS c ON b~lifnr = c~lifnr
          FIELDS COUNT( a~ebelp ) AS total_lineitem,
                 b~waers AS currency,
                 b~bukrs AS company_code,
                 b~ekorg AS po_org,
                 b~ekgrp AS Purchasing_Group,
                 b~aedat AS creation_date,
                 b~lifnr AS supplier,
                 a~peinh AS perunit,
                 concat_with_space( c~name1, c~name2 , 1 ) AS supplier_name,
                 CAST( SUM( a~menge * a~netpr ) AS DEC( 15, 2 ) ) AS total_amount
          WHERE a~ebeln = @purchase_order
          GROUP BY a~ebeln,b~waers,b~bukrs,b~ekorg,b~ekgrp,b~aedat,b~lifnr,c~name1, c~name2,a~peinh
          INTO TABLE @DATA(total_po_value).
*--- Header info
        SELECT SINGLE * FROM ekko INTO @DATA(ls_purchase_header_info) WHERE ebeln = @purchase_order. "#EC CI_ALL_FIELDS_NEEDED

        IF sy-subrc EQ 0.
          DATA(usertext) = zzcl_email_text=>get_username_text( username = username ).

*-- Currency Conversion
          DATA(po_details)      = total_po_value[ 1 ].
          DATA lv_amount TYPE char100.
          DATA tamount TYPE p DECIMALS 2.
*          IF po_details-perunit = 10.
          tamount = po_details-total_amount / po_details-perunit.
          WRITE tamount TO lv_amount CURRENCY po_details-currency .
          CONDENSE lv_amount.
*          WRITE po_details-total_amount TO lv_amount CURRENCY po_details-currency .
*          CONDENSE lv_amount.
          DATA(total_po_value_) = |{ lv_amount } { total_po_value[ 1 ]-currency }|.

*-- Total count of lines
          lv_total_lineitem   = po_details-total_lineitem.
          po_details-supplier = |{ po_details-supplier ALPHA = OUT }|.  " Supplier infomaation
*-- PO Date - DD.MM.YYYY
          lv_po_date          = |{ po_details-creation_date+6(2) }.{ po_details-creation_date+4(2) }.{ po_details-creation_date+0(4) }|.

*-- get comments
          CALL METHOD zzcl_email_text=>get_approval_comments
            EXPORTING
              workitem_id       = workitem_id
              ls_purchase_order = ls_purchase_header_info
            IMPORTING
              approval_comments = lv_approval_comments
              level             = lv_level.

*-- Read the call stack
          CALL FUNCTION 'SYSTEM_CALLSTACK'
            EXPORTING
              max_level    = 0
            IMPORTING
              callstack    = lt_callstack[]
              et_callstack = et_callstack[].
*-- In case of Rejection program we need to Append one level of approval
          IF line_exists( et_callstack[ progname = 'ZZREPT_MM_PO_EMAIL_REJECT' ] ).
            lv_level = lv_level + 1.
          ENDIF.
*-- replace all Variables
          lv_level_s = lv_level.
          REPLACE '&1' WITH  usertext INTO l_text_prolog.
          REPLACE '&2' WITH  purchase_order INTO l_text_prolog.
          REPLACE '&3' WITH  total_po_value_ INTO l_text_prolog.
          REPLACE '&4' WITH  lv_level_s INTO l_text_prolog.
          REPLACE '&5' WITH  lv_approval_comments INTO l_text_prolog.
          REPLACE '&6' WITH  workitem_id INTO l_text_prolog.
        ENDIF.
      ENDIF.
    ENDIF.
    CLEAR: lv_total_lineitem.

*-- Send email to Vendor, if overall PO is released.
    IF ls_purchase_header_info-frgke = 'R' OR ls_purchase_header_info-frgke = 'G'.
*-- Call Email functionality
      DATA lv_return TYPE char1.
      CALL FUNCTION 'ZZFM_MM_PO_EMAIL' DESTINATION 'NONE'
        EXPORTING
          iv_ebeln  = purchase_order
        IMPORTING
          ev_return = lv_return.
    ENDIF.

  ENDMETHOD.

----------------------------------------------------------------------------------
Extracted by Mass Download version 1.5.5 - E.G.Mellodew. 1998-2023. Sap Release 756
